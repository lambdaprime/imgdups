plugins {
  id 'com.diffplug.spotless'
  id 'java'
}

apply plugin : 'eclipse'

eclipse.classpath.file {
  // instruct eclipse plugin to put everything to module path
  whenMerged {
    entries.findAll { it.kind == 'lib' }.each { it.entryAttributes['module'] = 'true' }
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation fileTree(include: ['*.jar'], dir: '/usr/share/java')
}

configurations {
  customConfig.extendsFrom implementation
}

jar {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  manifest { 
    attributes 'Implementation-Version': "${rootProject.version}",
      'Main-Class': 'id.imgdups.app.ImgdupsApp'
  }
  from {
    configurations.customConfig.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

def packageDir = "$buildDir/package/${projectName}"

ext.artifacts = fileTree(include: ['*'], dir: "$packageDir/libs")

task createRelease(type: Zip) {
    archiveFileName = "$releaseFileName"
    destinationDirectory = file("$projectDir/release")
    from file("$packageDir/..")
}

task copyLibs(type: Copy) {
    from "$projectDir/libs"
    from jar.archiveFile
    into "${packageDir}/libs"
}

task copyReadme(type: Copy) {
  from("${project.rootDir}/README.md")
  into("$buildDir/resources/main")
}

task createPackage(type: Copy, dependsOn: copyLibs) {
    from "${project.rootDir}/README.md"
    from "${project.rootDir}/LICENSE-2.0.txt"
    from "$projectDir/src/main/imgdups"
    into packageDir
}

task cleanRelease(type: Delete) {
  delete "$packageDir"
  delete "$projectDir/release/*-SNAPSHOT.zip"
}

jar.dependsOn copyReadme
createRelease.dependsOn createPackage
assemble.dependsOn createRelease
clean.dependsOn cleanRelease
// run spotless and format code before the build
classes.dependsOn spotlessApply
