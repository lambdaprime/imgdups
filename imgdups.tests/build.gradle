plugins {
  id 'com.diffplug.spotless'
  id 'jacoco'
}

eclipse.classpath.file {
  // instruct eclipse plugin to put everything to module path
  whenMerged {
    entries.findAll { it.kind == 'lib' }.each { it.entryAttributes['module'] = 'true' }
  }
}

dependencies {
  testImplementation project(':imgdups')

  def junitVersion = "5.8.1"
  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
  testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
  testRuntimeOnly "org.junit.platform:junit-platform-commons:1.7.0"
}

jacoco {
  // Java 17 support
  toolVersion = "0.8.7"
}

String testDir = "$buildDir/${projectName}"
String testSrcDir = "$projectDir/src/test"

// Gradle has no UnZip task so we reuse Copy for that
task myTestCopy(type: Copy) {
    mkdir file(testDir)
    from zipTree("${project.rootDir}/${projectName}/release/${releaseFileName}")
    into "$buildDir"
}

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport, jacocoTestCoverageVerification

  testLogging {
    events "started", "failed"
  }
}

jacocoTestReport {
  // generate only HTML report
  reports {
    xml.required = false
    csv.required = false
    html.required = true
  }
  sourceDirectories.from = project(':imgdups').sourceSets.main.java.srcDirs
  classDirectories.from = project(':imgdups').sourceSets.main.java.classesDirectory
}

jacocoTestCoverageVerification {
  sourceDirectories.from = project(':imgdups').sourceSets.main.java.srcDirs
  classDirectories.from = project(':imgdups').sourceSets.main.java.classesDirectory
  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude:  [
        // none
      ])
    }))
  }
  violationRules {
    rule {
      enabled = true
      element = 'PACKAGE'
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 0.0
      }
    }
  }
}

// run spotless and format code before the build
testClasses.dependsOn spotlessApply
myTestCopy.dependsOn project(':imgdups').createRelease

compileTestJava.dependsOn myTestCopy
jar.dependsOn myTestCopy